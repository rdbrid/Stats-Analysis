OPTIONS LS=80 NODATE;
TITLE 'EXACT RIDGE PRESS METHOD';

DATA HOSPITAL;
INPUT X1 X2 X3 X4 X5 Y;
CARDS;
  15.57  2463   472.92   18.0  4.45   566.52
  44.02  2048  1339.75    9.5  6.92   696.82
  20.42  3940   620.25   12.8  4.28  1033.15
  18.74  6505   568.33   36.7  3.90  1603.62
  49.20  5723  1497.60   35.7  5.50  1611.37
  44.92 11520  1365.83   24.0  4.60  1613.27
  55.48  5779  1687.00   43.3  5.62  1854.17
  59.28  5969  1639.92   46.7  5.15  2160.55
  94.39  8461  2872.33   78.7  6.18  2305.58
 128.02 20106  3655.08  180.5  6.15  3503.93
  96.00 13313  2912.00   60.9  5.88  3571.89
 131.42 10771  3921.00  103.7  4.88  3741.40
 127.21 15543  3865.67  126.8  5.50  4026.52
 252.90 36194  7684.10  157.7  7.00 10343.81
 409.20 34703 12446.33  169.4 10.78 11732.17
 463.70 39204 14098.40  331.4  7.05 15414.94
 510.22 86533 15524.00  371.6  6.35 18854.45
;

* MACRO VARLIST CONTAINS THE NAMES OF THE REGRESSOR VARIABLES;
* MACRO DEPVAR CONTAINS THE NAME OF THE DEPENDENT VARIABLE;
* MACRO DATASET CONTAINS THE NAME OF THE DATA SET CONTAINING THESE
  VARIABLES;
* MACRO AMT IS THE DESIRED INCREMENT ON k FOR RIDGE PRESS;

MACRO VARLIST X1 X2 X3 X4 X5 %
MACRO DEPVAR Y %
MACRO DATASET HOSPITAL  %
MACRO AMT .0100  %

MACRO RPEXACT

PROC IML;
USE DATASET;READ ALL INTO X(|COLNAME=XNAMES|) VAR {VARLIST};
READ ALL INTO Y(|COLNAME=YNAME|) VAR {DEPVAR};
XY=X||Y;* XY IS THE COMPLETE DATASET;
VARNAMES=XNAMES||YNAME;  PRINT XY (|COLNAME=VARNAMES|);
* N IS THE NUMBER OF OBSERVATIONS IN THE DATA SET;
* NV IS THE NUMBER OF REGRESSOR VARIABLES INCLUDED;
*  THE X"S ARE CENTERED AND SCALED;
N=NROW(XY);NV=NCOL(XY)-1;P1=NV+1;IP=1:NV;
* ADD IS THE INCREMENT ON K .....TOP IS THE NUMBER OF K VALUES;
INC=.00; ADD=AMT;      TOP=25;
* THE NEXT LINE INITIALIZES THE MATRICES FOR FUTURE USE;
PR=J(25,2,0);N1=N;N=N-1;RES=J(N1,1,0);XX=J(N,NV);YY=J(N,1);
* THE FOLLOWING LOOP CREATES AN X MATRIX AND A Y VECTOR WITHOUT THE
OBSERVATION WE ARE GOING TO PREDICT;
START RIDGE;
L=1;LOOP:IF L>TOP THEN GO TO FIN; PR(|L,1|)=INC;
K=0;LOP:K=K+1;IF K>N1 THEN GO TO LAP;
LA=0; DO IR=1 TO N1; IF IR ^= K THEN DO;
LA=LA+1;DO IC=1 TO NV; XX(|LA,IC|)=X(|IR,IC|);END;
YY(|LA,1|)=Y(|IR,1|);END;END;
MEAN=XX(|+, |)/(N);
*MEAN IS THE MEAN OF EACH X VECTOR WITHOUT THE KTH OBSERVATION;
YBAR=SUM(YY)/(N);
*MEAN IS THE MEAN OF THE Y VECTOR WITHOUT THE KTH OBSERVATION;
XX=XX-J(N,1)*MEAN; * XX IS THE X MATRIX CORRECTED FOR THE MEAN;
SS=XX(|##, |); STD=SQRT(SS`);XX=XX*DIAG(1/STD);
*SS IS THE CORRECTED SUMS OF SQUARES OF THE X'S;
*XX IS THE STANDARIZED X MATRIX;
YOBS=Y(|K,1|);XOBS=X(|K,|);XI=1;XOBS=XI||XOBS;Z=XX`*YY;
*Z IS THE X PRIME Y VECTOR STANDARDIZED;
ID=I(NV);KK=INC#ID; XPX=(XX`*XX)+KK;XIX=INV(XPX); B=XIX*Z;
*B IS THE RIDGE ESTIMATOR FOR A GIVEN INC WITHOUT THE KTH OBSERVATION ;
* INC OR KK IS THE RIDGE PARAMETER;
TB=B/STD;
*T IS A VECTOR OF T-VALUES FOR TESTING WHETHER THE B ESTIMATES ARE;
*DIFFERENT FROM ZERO;
B0=TB`*MEAN`;INTERCPT=YBAR-B0;TB=INTERCPT//TB;
*TB IS THE UNSTANDARDIZED B VECTOR;
* THE B VECTOR SHOULD NOT BE PRINTED THEY ARE NOT CORRECT SINCE DATA IS
BEING THROWN OUT;
R=YOBS-XOBS*TB;RES(|K,1|)=R;R=R#R;PR(|L,2|)=PR(|L,2|)+R;
*PR IS ADDING THE SQUARED RESIDUALS IN THE ORIGINAL UNITS;
GO TO LOP; LAP: INC=INC+ADD;L=L+1; ;GO TO LOOP;
FIN:  FINISH;  RUN RIDGE;
NAMES={K PRESS} ;
CREATE KPLOT  FROM PR(|COLNAME=NAMES|);   APPEND FROM PR;
RUN ;
PROC PRINT DATA=KPLOT; TITLE 'EXACT RIDGE PRESS METHOD';
PROC PLOT;PLOT PRESS*K='P';
%

RPEXACT;

quit;
