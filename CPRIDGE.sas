OPTIONS LS=80 NODATE;
TITLE 'RIDGE REGRESSION USING CPRIDGE';

DATA HOSPITAL;
INPUT X1 X2 X3 X4 X5 Y;
CARDS;
  15.57  2463   472.92   18.0  4.45   566.52
  44.02  2048  1339.75    9.5  6.92   696.82
  20.42  3940   620.25   12.8  4.28  1033.15
  18.74  6505   568.33   36.7  3.90  1603.62
  49.20  5723  1497.60   35.7  5.50  1611.37
  44.92 11520  1365.83   24.0  4.60  1613.27
  55.48  5779  1687.00   43.3  5.62  1854.17
  59.28  5969  1639.92   46.7  5.15  2160.55
  94.39  8461  2872.33   78.7  6.18  2305.58
 128.02 20106  3655.08  180.5  6.15  3503.93
  96.00 13313  2912.00   60.9  5.88  3571.89
 131.42 10771  3921.00  103.7  4.88  3741.40
 127.21 15543  3865.67  126.8  5.50  4026.52
 252.90 36194  7684.10  157.7  7.00 10343.81
 409.20 34703 12446.33  169.4 10.78 11732.17
 463.70 39204 14098.40  331.4  7.05 15414.94
 510.22 86533 15524.00  371.6  6.35 18854.45
;

* MACRO VARLIST CONTAINS THE NAMES OF THE REGRESSOR VARIABLES;
* MACRO DEPVAR CONTAINS THE NAME OF THE DEPENDENT VARIABLE ;
* MACRO DATASET CONTAINS THE NAME OF THE DATA SET CONTAINING THESE
  VARIABLES;
* MACRO COEF CONTAINS THE NAMES OF THE REGRESSION COEFFICIENTS;

MACRO VARLIST X1 X2 X3 X4 X5  %
MACRO DEPVAR Y %
MACRO DATASET HOSPITAL  %
MACRO COEF {'B0' 'B1' 'B2' 'B3' 'B4' 'B5'} %

* SET KINC EQUAL TO THE DESIRED INCREMENT FOR K ;
* KSTART IS THE SMALLEST K THAT WILL BE USED;
* KSTOP IS THE LARGEST K THAT WILL BE USED;

MACRO CPRIDGE

PROC IML;
USE DATASET;
READ ALL INTO X(|COLNAME=XNAMES|) VAR {VARLIST};
USE DATASET;READ ALL INTO Y(|COLNAME=YNAME|) VAR {DEPVAR};
XY=X||Y;
VARNAME=XNAMES||YNAME;
KSTART ={0.000};   KSTOP ={0.020};    KINC ={0.001};
KEND = KSTOP + KINC;
KTOTAL = ((KEND - KSTART)/KINC);
K = KSTART;      L={0};
N=NROW(X);    * N IS THE NUMBER OF OBSERVATIONS;
P=NCOL(X);    * P IS THE NUMBER OF VARIABLES;
*THE FOLLOWING TWO MATRICES ARE BEING INITIALIZED TO ZERO FOR FUTURE USE;
CRIDGE = J(KTOTAL,{2});   RESULT = J(KTOTAL,P+{3});
* * * * * * CENTER AND SCALE THE X DATA * * * * * * * *;
MEAN =X (|+, |)/N;  X=X - J(N,{1})*MEAN;  SS=X(|##, |);
X=X*DIAG({1}/SQRT(SS`));
* * * * * * CENTER THE Y DATA * * * * * * * * * * * * *;
YBAR = Y(|+,|)/N;  Y = Y - YBAR#J(N,{1});
* * * * * * COMPUTE CRIDGE * * * * * * * * * * * * * *;
XTX = X`*X;   H=X*INV(XTX)*X`;
RESOLS = Y`*(I(N) - H)*Y;
* RESOLS IS THE RESIDUAL SUM OF SQUARES FOR ORDINARY LEAST SQUARES;
START CPR;  DO WHILE (K < KEND);
A=X*INV(XTX + K#I(P))*X`;  * A IS A HAT LIKE MATRIX;
SSRES = Y`*((I(N)-A)**{2})*Y;
*SSRES IS SIMILAR TO THE RESIDUAL SUM OF SQUARES;
L=L +{1};  CRIDGE(|L,{1}|)=K;
CRIDGE(|L,{2}|) = {2}#TRACE(A) - N + SSRES/RESOLS #(N-(P+{1}))+{2};
* THE FIRST COLUMN OF CPRIDGE CONTAINS THE K;
* THE SECOND COLUMN OF CPRIDGE CONTAINS THE CPRIDGE STATISTIC;
* * * * * * COMPUTE REGRESSION COEFFICIENTS * * * * * *;
BSCALED = INV(XTX + K#I(P))*X`*Y;
* BSCALED CONTAINS THE CPRIDGE COEFFICIENTS;
B= BSCALED`*DIAG(1/SQRT(SS`));
* B IS THE UNTRANSFORMED REGRESSION COEFFICIENTS;
INTERCPT =YBAR -B*MEAN`;  *INTERCPT IS THE UNTRANSFORMED INTERCEPT;
RESULT(|L,|) = CRIDGE(|L,|)||INTERCPT||B;
K = K + KINC;           END;  FINISH;  RUN CPR;
* RESULT HAS CPRIDGE THE INTERCEPT AND THE REGRESSION COEFFICIENTS
FOR EACH K;
*NAMES={'K' 'CPRIDGE' 'B0'}||{VARLIST};
NAMES={'K' 'CPRIDGE'}||COEF;
PRINT RESULT (|COLNAME=NAMES|);
CREATE  RESULTS FROM RESULT(|COLNAME=NAMES|); APPEND FROM RESULT;
TITLE 'RIDGE REGRESSION USING CPRIDGE';
* TO HAVE THE PLOT INTERACTIVELY ON THE SCREEN IN GRAPHICS MODE REMOVE
THE FOLLOWING SYMBOLS;
/*
GOPTIONS GPROTOCOL=GSAS7171;
CALL GSTART;
CALL GYAXIS({15 15},80,CPRIDGE,5);
CALL GXAXIS ({15 15},80,K,5,6.3);
CALL GPOINT (K,CPRIDGE);
LABELS={'K'  'CPRIDGE'};
CALL GSCRIPT(50,5,LABELS(|1|));
CALL GSCRIPT(3,60,LABELS(|2|)) ANGLE=270 ROTATE=90;
CALL GSHOW;
QUIT; */
PROC PLOT DATA=RESULTS;PLOT CPRIDGE*K='*';
%

CPRIDGE;

quit;
